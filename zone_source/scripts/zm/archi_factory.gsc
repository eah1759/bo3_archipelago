#using scripts\codescripts\struct;
#using scripts\shared\flag_shared;
#using scripts\shared\system_shared;
#using scripts\shared\array_shared;
#using scripts\shared\exploder_shared;
#using scripts\shared\util_shared;
#using scripts\shared\player_shared;
#using scripts\shared\callbacks_shared;
#using scripts\shared\hud_shared;
#using scripts\shared\hud_message_shared;
#using scripts\shared\hud_util_shared;
#using scripts\shared\lui_shared;
#using scripts\shared\clientfield_shared;
#using scripts\zm\_zm_perks;
#using scripts\zm\_zm_score;
#using scripts\zm\_zm_weapons;
#using scripts\zm\craftables\_zm_craftables;

#using scripts\zm\archi_core;
#using scripts\zm\archi_save;

#insert scripts\shared\shared.gsh;
#insert scripts\shared\version.gsh;

#insert scripts\zm\archi_core.gsh;

function save_state_manager()
{
    level.archi.save_state = &save_state;
    level thread archi_save::save_on_round_change();
    level thread archi_save::round_checkpoints();
    level waittill("end_game");

    if (isdefined(level.host_ended_game) && level.host_ended_game == 1)
    {
        IPrintLn("Host ended game, saving data...");
        save_state();
    } else {
        IPrintLn("Host did not end game, clearing data...");
        clear_state();
    }
}

function save_state()
{
    archi_save::save_round_number();
    archi_save::save_zombie_count();
    archi_save::save_power_on();
    archi_save::save_doors_and_debris();
    archi_save::save_spent_tokens();

    archi_save::save_players(&save_player_data);

    save_map_state();

    archi_save::send_save_data("zm_factory");

    if (level.archi.save_checkpoint == true)
    {
        IPrintLnBold("Checkpoint Saved");
    }
}

// self is player
function save_player_data(xuid)
{  
    self archi_save::save_player_score(xuid);
    self archi_save::save_player_perks(xuid);
    self archi_save::save_player_loadout(xuid);
}

function load_state()
{
    level.archi.zm_castle_landingpads = 0;
    archi_save::wait_restore_ready("zm_factory");
    level flag::wait_till("ap_attachment_rando_ready");
    archi_save::restore_spent_tokens();
    archi_save::restore_zombie_count();
    archi_save::restore_round_number();
    archi_save::restore_power_on();
    archi_save::restore_doors_and_debris();

    restore_map_state();

    wait(10);
    level flag::clear("ap_prevent_checkpoints");
}

// self is player
function restore_player_data(xuid)
{
    level endon("end_game");
    self endon("disconnect");

    if (self archi_save::can_restore_player(xuid))
    {
        self archi_save::restore_player_score(xuid);
        self archi_save::restore_player_perks(xuid);
        self archi_save::restore_player_loadout(xuid);
    }
}

function clear_state()
{
    SetDvar("ARCHIPELAGO_CLEAR_DATA", "zm_factory");
    LUINotifyEvent(&"ap_clear_data", 0);
}

function setup_locations()
{
    level flag::wait_till("initial_blackscreen_passed");

    level thread _track_music_boa();
    level thread _any_teleporter_linked();
    level thread _all_teleporters_linked();
    level thread _flytrap_targets_shot();
    level thread _flag_to_location_thread("snow_ee_completed", level.archi.mapString + " Reveal the Secret Perk Machine");
}

function _track_music_boa()
{
    level waittill("hash_a1b1dadb");
    archi_core::send_location(level.archi.mapString + " Music EE - Beauty of Annihilation Remix");
}

function _any_teleporter_linked()
{
    level flag::wait_till_any(array("teleporter_pad_link_1", "teleporter_pad_link_2", "teleporter_pad_link_3"));
    archi_core::send_location(level.archi.mapString + " Link a Teleporter");
}

function _all_teleporters_linked()
{
    level flag::wait_till_all(array("teleporter_pad_link_1", "teleporter_pad_link_2", "teleporter_pad_link_3"));
    archi_core::send_location(level.archi.mapString + " Link All 3 Teleporters");
}

function _flytrap_targets_shot()
{
    level flag::wait_till_all(array("ee_perk_bear", "ee_bowie_bear", "ee_exp_monkey"));
    archi_core::send_location(level.archi.mapString + " Fly Trap Quest");
}

function _flag_to_location_thread(flag, location)
{
    level endon("end_game");

    level flag::wait_till(flag);
    archi_core::send_location(location);
}

function save_map_state()
{
    archi_save::save_flag("teleporter_pad_link_1");
    archi_save::save_flag("teleporter_pad_link_2");
    archi_save::save_flag("teleporter_pad_link_3");
}

function restore_map_state()
{
    archi_save::restore_flag("teleporter_pad_link_1");
    archi_save::restore_flag("teleporter_pad_link_2");
    archi_save::restore_flag("teleporter_pad_link_3");
}